plugins {
    id "com.jfrog.bintray" version "1.7.1"
}

apply plugin: 'java'
apply from: 'http://dl.bintray.com/content/shemnon/javafx-gradle/8.1.1/javafx.plugin'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'checkstyle'

group = 'org.hildan'
version = '2.1.0'
sourceCompatibility = 1.8
description = 'A JavaFX program to view log files in a beautiful way.'
ext.labels = ['log', 'viewer']

def mainClassName = "org.hildan.fxlog.FXLog"

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compile 'org.controlsfx:controlsfx:8.40.12'
    compile 'org.fxmisc.easybind:easybind:1.0.3'
    compile 'commons-io:commons-io:2.5'
    compile 'com.google.code.gson:gson:2.7'
    compile 'org.hildan.fxgson:fx-gson:1.2.2'
    compile 'org.jetbrains:annotations:15.0'
    testCompile 'junit:junit:4.+'
}

// to inject the project version in about.fxml and .properties files
processResources {
    ['**/*.properties', '**/about.fxml'].each {
        filesMatching(it) {
            f -> f.expand(projectVersion: project.version, buildDate: getDate())
        }
    }
}

def static getDate() {
    def date = new Date()
    def formattedDate = date.format('MMM d, yyyy')
    return formattedDate
}

// to build a fat jar with all dependencies included
jar {
    manifest { attributes "Main-Class": mainClassName }
    from {
        configurations.compile.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
}

// defines JavaFX deployment info
javafx {
    appID 'FX-Log'
    appName 'FX Log'
    vendor = "Hildan Software"
    mainClass mainClassName

    profiles {
        windows {
            category = 'Utils'
        }
    }

    copyright = 'Copyright (c) 2016 Joffrey Bion'
    description = project.description
    licenseType = 'MIT'
    installSystemWide = true
    menu = true
    shortcut = true
}

apply from: 'gradle/scripts/bintray.gradle'
apply from: 'gradle/scripts/release.gradle'
apply from: 'gradle/scripts/checkstyle.gradle'